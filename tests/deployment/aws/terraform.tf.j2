terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 5.92"
    }
  }

  required_version = ">= 1.2"
}

provider "aws" {
  region = "us-east-1"
}

resource "aws_security_group" "allow_all_outbound" {
  name        = "allow_all_outbound"
  description = "Allow all outbound traffic"

  tags = {
    Name = "Allow All Outbound"
  }
}

resource "aws_vpc_security_group_egress_rule" "allow_all_outbound_ipv4" {
  security_group_id = aws_security_group.allow_all_outbound.id
  description       = "Allow all outbound IPv4 traffic"
  cidr_ipv4         = "0.0.0.0/0"
  ip_protocol       = -1

  tags = {
    Name = "Allow All IPv4"
  }
}

resource "aws_vpc_security_group_egress_rule" "allow_all_outbound_ipv6" {
  security_group_id = aws_security_group.allow_all_outbound.id
  description       = "Allow all outbound IPv6 traffic"
  cidr_ipv6         = "::/0"
  ip_protocol       = -1

  tags = {
    Name = "Allow All IPv6"
  }
}

resource "aws_security_group" "allow_ssh" {
  name        = "allow_ssh"
  description = "Allow SSH inbound traffic"

  tags = {
    Name = "Allow SSH"
  }
}

resource "aws_vpc_security_group_ingress_rule" "allow_ssh_ipv4" {
  security_group_id = aws_security_group.allow_ssh.id
  description       = "Allow incoming traffic for SSH over IPv4"
  cidr_ipv4         = "0.0.0.0/0"
  from_port         = 22
  ip_protocol       = "tcp"
  to_port           = 22

  tags = {
    Name = "Allow SSH IPv4"
  }
}

resource "aws_vpc_security_group_ingress_rule" "allow_ssh_ipv6" {
  security_group_id = aws_security_group.allow_ssh.id
  description       = "Allow incoming traffic for SSH over IPv6"
  cidr_ipv6         = "::/0"
  from_port         = 22
  ip_protocol       = "tcp"
  to_port           = 22

  tags = {
    Name = "Allow SSH IPv6"
  }
}

resource "aws_security_group" "allow_https" {
  name        = "allow_https"
  description = "Allow HTTPS inbound traffic"

  tags = {
    Name = "Allow HTTPS"
  }
}

resource "aws_vpc_security_group_ingress_rule" "allow_https_ipv4" {
  security_group_id = aws_security_group.allow_https.id
  description       = "Allow incoming traffic for HTTPS over IPv4"
  cidr_ipv4         = "0.0.0.0/0"
  from_port         = 443
  ip_protocol       = "tcp"
  to_port           = 443

  tags = {
    Name = "Allow HTTPS IPv4"
  }
}

resource "aws_vpc_security_group_ingress_rule" "allow_https_ipv6" {
  security_group_id = aws_security_group.allow_https.id
  description       = "Allow incoming traffic for HTTPS over IPv6"
  cidr_ipv6         = "::/0"
  from_port         = 443
  ip_protocol       = "tcp"
  to_port           = 443

  tags = {
    Name = "Allow HTTPS IPv6"
  }
}

resource "aws_security_group" "allow_wg" {
  name        = "allow_wg"
  description = "Allow WireGuard inbound traffic"

  tags = {
    Name = "Allow WireGuard"
  }
}

resource "aws_vpc_security_group_ingress_rule" "allow_wg_ipv4" {
  security_group_id = aws_security_group.allow_wg.id
  description       = "Allow incoming traffic for WireGuard over IPv4"
  cidr_ipv4         = "0.0.0.0/0"
  from_port         = 51820
  ip_protocol       = "udp"
  to_port           = 51820

  tags = {
    Name = "Allow WireGuard IPv4"
  }
}

resource "aws_vpc_security_group_ingress_rule" "allow_wg_ipv6" {
  security_group_id = aws_security_group.allow_wg.id
  description       = "Allow incoming traffic for WireGuard over IPv6"
  cidr_ipv6         = "::/0"
  from_port         = 51820
  ip_protocol       = "udp"
  to_port           = 51820

  tags = {
    Name = "Allow WireGuard IPv6"
  }
}

data "aws_ami" "debian" {
  most_recent = true

  filter {
    name   = "name"
    values = ["debian-13-amd64-*"]
  }

  owners = ["136693071363"] # Debian
}

resource "aws_key_pair" "wg_quickrs_auto_generated_keypair" {
  key_name   = "wg_quickrs_auto_generated_keypair"
  public_key = "{{ ssh_public_key }}"
}

resource "aws_instance" "wg_quickrs_server" {
  ami           = data.aws_ami.debian.id
  instance_type = "t2.nano"
  key_name      = aws_key_pair.wg_quickrs_auto_generated_keypair.id
  vpc_security_group_ids = [aws_security_group.allow_all_outbound.id, aws_security_group.allow_ssh.id, aws_security_group.allow_https.id, aws_security_group.allow_wg.id]

  tags = {
    Name = "wg-quickrs-server"
  }
}

output "instance_public_ip" {
  value = aws_instance.wg_quickrs_server.public_ip
}
